@precedence {
  xend @left,
  xmid @left,
  member @left,
  compon @left,
  index @left,
  funcall @left,
  exp @left,
  sign @right,
  seq @left,
  infix @left,
  times @left,
  plus @left,
  comp @left,
  not @left,
  and @left,
  or @left,
  frmla @left,
  rassign @left,
  lassign @right,
  eqassign @right,
  if,
  else @right,
  repeat,
  while,
  for,
  in,
  funbody,
  paren,
  help,
  block
}

kw<term> { @specialize[@name={term}]<simpleName, term> }

ifKwd[@export] { kw<"if"> }
elseKwd { kw<"else"> }
repeatKwd { kw<"repeat"> }
whileKwd[@export] { kw<"while"> }
functionKwd[@export] { kw<"function"> }
forKwd[@export] { kw<"for"> }
inKwd { kw<"in"> }
nextKwd { kw<"next"> }
breakKwd { kw<"break"> }
trueKwd { kw<"TRUE"> }
falseKwd { kw<"FALSE"> }
nullKwd { kw<"NULL"> }
infKwd { kw<"Inf"> }
nanKwd { kw<"NaN"> }
naKwd { kw<"NA"> }
naintKwd { kw<"NA_integer_"> }
narealKwd { kw<"NA_real_"> }
nacomplexKwd { kw<"NA_complex_"> }
nacharKwd { kw<"NA_character_"> }

@skip { spaces | Comment | snl }

lineCell { ( expression ";"? )+ !xend hnl }
ScriptLineCell { lineCell }
ScriptEndCell { ( expression ";"? )+ eof }
scriptEnd { eof }

BlockLineCell { lineCell }
BlockEndCell { ( expression ";"? )+ closeBrace }
blockEnd { closeBrace }
BlockCell { blockLineCell | blockCell }

@top Script { ( ScriptLineCell | hnl )* (ScriptEndCell | scriptEnd) }

Block { openBrace ( BlockLineCell | hnl )* (BlockEndCell | blockEnd) }

nlsok { !xmid hnl* }

expression {
   literal | Identifier | call
}

literal { 
  Character | Numeric | Integer | Logical | Complex
}

Identifier { 
  simpleName | backtickName   
}

call {
    UnaryExpr | BinaryExpr| ControlExpr | IndexExpr |
    StdCall | FuncDef |
    Block | ParenExpr
}

ParenExpr { !paren openParen nlsok expression nlsok closeParen }

UnaryExpr {
    !sign ArithOp< "+" | "-" > nlsok expression | 
    !not LogicOp< "!" > nlsok expression |
    !frmla GenOp< "~" > nlsok expression 
}

BinaryExpr { 
    expression !seq GenOp< ":" > nlsok expression |
    expression !times ArithOp< "*" | "/" > nlsok expression |
    expression !plus ArithOp< "+" | "-" > nlsok expression |
    expression !exp ArithOp< "^" > nlsok expression |
    expression !infix GenOp< infix > nlsok expression |
    expression !comp CompOp< "<" | ">" | ">=" | "<=" | "==" | "!=" > nlsok expression |
    expression !and LogicOp< "&" | "&&" > nlsok expression |
    expression !or LogicOp< "|" | "||" > nlsok expression |
    expression !lassign AssignOp< "<-" | "<<-" > nlsok expression |
    expression !rassign AssignOp< "->" | "->>" > nlsok expression |
    expression !frmla GenOp< "~" > nlsok expression | 
    expression !compon GenOp< "$" | "@" > nlsok ( Identifier | Character ) |
    Identifier !member GenOp< "::" | ":::" > nlsok Identifier
}

ArithOp<expr> { expr }
CompOp<expr> { expr }
LogicOp<expr> { expr }
AssignOp<expr> { expr }
GenOp<expr> { expr }

EmptyArgValue { ~emptylist ~emptyvalue }
ArgValue { ~emptyvalue ( Identifier "=" )? expression }
EmptyArgList { ~emptylist }
ArgList { 
  ArgValue ( "," ( ArgValue | EmptyArgValue) )*  | 
  EmptyArgValue  ( "," ( ArgValue | EmptyArgValue) )+  
}
StdCall { expression !funcall openParen ( EmptyArgList | ArgList ) closeParen }

IndexExpr {
  expression !index openBrack ( EmptyArgList | ArgList ) closeBrack |
  expression !index openDBrack ( EmptyArgList | ArgList ) closeDBrack
}

commaSep<x> { "" | x ( "," x)* }

ParamValue { Identifier ( "=" expression)? }
ParamList { commaSep<ParamValue>  }
FuncDef { functionKwd openParen ParamList closeParen !funbody expression }

ifExpr { ifKwd nlsok openParen nlsok expression  nlsok closeParen nlsok !if expression ( !else elseKwd nlsok expression)? } 

repeatExpr { repeatKwd nlsok !repeat expression }
whileExpr { whileKwd nlsok openParen nlsok expression nlsok closeParen nlsok !while expression }
forExpr { forKwd nlsok openParen nlsok Identifier nlsok inKwd nlsok !in expression nlsok ")" nlsok !for expression }

ControlExpr {
  ifExpr | repeatExpr | whileExpr | forExpr
}

infix { infix0 | infix1 | infix2 | infix3 | infix4 | infix5 | infix6 | infix7| infix8 | infix9 }

@tokens {
  spaces[@export]  { $[\u0009 \u000b\u00a0\u1680\u2000-\u200a\u202f\u205f\u3000\ufeff]+ }
  newline[@export]  { $[\r\n\u2028\u2029] }
  eof { @eof }
  identifierChar { @asciiLetter }

  @precedence { spaces, newline, identifierChar }

  simpleName {
    ( identifierChar | "." ) ( ( identifierChar | "." | "_" ) ( identifierChar | @digit | "." | "_" )* )?
  }

  Character {
    '"' ( ![\\\n"] | '\\' '"' )*  '"' |
    "'" ( ![\\\n'] | "\\" "'" )*  "'"
  }

  Logical { "TRUE" | "FALSE" }

  number {
    ( ( (@digit+ ( "." @digit* )? ) | ( "." @digit+ ) )  ( ("e" | "E") ("+" | "-")? @digit+ )? ) |
    ( ( "0x" | "0X" )( @digit | $[a-fA-F] )+ ( "."? ( @digit | $[a-fA-F] )* ("p" | "P") ("+" | "-")? @digit+ )? )
  }

  Numeric { number }

  Integer { number "L" }

  Complex { number "i" }

  backtickName { "`" ![\n]* "`" }

  Comment { "#" ![\n]* }
  
  infix0 { "%%" }
  infix1 { "%" ![%] "%" }
  infix2 { "%" ![%] ![%] "%" }
  infix3 { "%" ![%] ![%] ![%]  "%" }
  infix4 { "%" ![%] ![%] ![%] ![%] "%" }
  infix5 { "%" ![%] ![%] ![%] ![%] ![%] "%" }
  infix6 { "%" ![%] ![%] ![%] ![%] ![%] ![%] "%" }
  infix7 { "%" ![%] ![%] ![%] ![%] ![%] ![%] ![%] "%" }
  infix8 { "%" ![%] ![%] ![%] ![%] ![%] ![%] ![%] ![%] "%" }
  infix9 { "%" ![%] ![%] ![%] ![%] ![%] ![%] ![%] ![%] ![%] "%" }

  openBrace[@export] { "{" }
  closeBrace[@export] { "}" }
  openBrack[@export, @name="[" ] { "[" }
  closeBrack[@export, @name="]"] { "]" }
  openDBrack[@export, @name="[[" ] { "[[" }
  closeDBrack[@export, @name="]]"] { "]]" }
  openParen[@export] { "(" }
  closeParen[@export] { ")" }

  ":"
  "::" ":::"
  "$" "@"
  "*" "/"
  "+" "-"
  "^"
  "!"
  "~"
  "<" ">" ">=" "<=" "==" "!="
  "&" "&&" "|" "||"
  "<-" "<<-" "->" "->>" "="

  @precedence {
    infix0
    infix1,
    infix2,
    infix3,
    infix4,
    infix5,
    infix6,
    infix7,
    infix8,
    infix9,
    backtickName,
    Logical,
    Character,
    Complex,
    Integer,
    Numeric,
    simpleName,
    ":::",
    "::",
    ":",
    "<<-",
    "<-",
    "->>",
    "->",
    "<=",
    ">=",
    "==",
    "<",
    ">",
    "=",
    "&&",
    "||",
    "&",
    "|",
    openDBrack,
    "[",
    closeDBrack,
    closeBrack
  }
}

@context trackDelims from "./tokens.js"
@external specialize {newline} replaceNewline from "./tokens" { hnl snl }


